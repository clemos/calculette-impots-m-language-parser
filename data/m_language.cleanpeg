comment = r'\#.*'

symbol = r'\w+'
symbol_enumeration = symbol ("," symbol)*
float = r'\d+\.\d+'
integer = r'\d+'
string = '"' r'[^"]*' '"'
integer_range = integer ".." integer
brackets = "[" symbol "]"

symbol_or_integer_range = (integer_range / symbol)
loop_variable_domains = symbol_or_integer_range ("," symbol_or_integer_range)*
loop_variable1 = "un" symbol "dans" loop_variable_domains
loop_variable2 =  symbol "=" loop_variable_domains
loop_variables = (loop_variable1 ("et" loop_variable1)*) / (loop_variable2 (";" loop_variable2)*)

application = "application" symbol ";"
applications_reference = "application" ":" symbol_enumeration
enchaineur = "enchaineur" symbol applications_reference ";"

alias = "alias" symbol
attribute = symbol "=" integer
tableau = "tableau[" integer "]"
type = "type" ("BOOLEEN" / "DATE_AAAA" / "DATE_JJMMAAAA" / "DATE_MM" / "ENTIER" / "REEL")
variable_calculee_tags = ("base" / "restituee")*
variable_calculee = symbol ":" tableau? "calculee" variable_calculee_tags ":" string type? ";"
variable_const = symbol ":" "const" "=" float ";"
variable_saisie = symbol ":" "saisie" ("contexte" / "famille" / "penalite" / "revenu")+ attribute* "restituee"? alias ":" string type? ";"
variable = (variable_calculee / variable_const / variable_saisie)

litteral = float / (integer !r'\w') / symbol
loop_expression = loop_variables ":" expression
function_parameters = expression ("," expression)*
function_call = symbol "(" (loop_expression / function_parameters) ")"
ternary_operator = "si" expression "alors" expression ("sinon" expression)? "finsi"
unary = ("+" / "-")
group = "(" expression ")"
factor = unary? (("pour" loop_expression) / ternary_operator / function_call / (symbol brackets) / litteral / group)
product_expression = factor (("*" / "/") factor)*
sum_expression = product_expression (("+" / "-") product_expression)*
comparaison = sum_expression ((">=" / "<=" / "!=" / ">" / "<" / "=") sum_expression)?
dans = comparaison (("non"? "dans") "(" loop_variable_domains ")")?
expression = dans (("et" / "ou") dans)*

formula = symbol brackets? "=" expression ";"
pour_formula = "pour" loop_variables ":" formula
enchaineur_reference = "enchaineur" ":" symbol ";"
regle = "regle" symbol+ ":" applications_reference ";" enchaineur_reference? (pour_formula / formula)+

verif_condition = "si" expression "alors" "erreur" symbol* ";"
verif = "verif" symbol+ ":" applications_reference ";" verif_condition+

erreur_type = ("anomalie" / "discordance" / "informative")
erreur = symbol ":" erreur_type ":" string ":" string ":" string ":" string ":" string ";"

m_source_file = (comment / application / enchaineur / variable / regle / verif / erreur)+ EOF
